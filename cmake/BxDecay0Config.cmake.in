# - Config file for the BxDecay0 package
# It defines the following variables
#  BxDecay0_VERSION       - BxDecay0 version

#----------------------------------------------------------------------
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#----------------------------------------------------------------------

set(BxDecay0_VERSION @BxDecay0_VERSION@)
set(BxDecay0_CXX_STANDARD @CMAKE_CXX_STANDARD@)
set(BxDecay0_WITH_GEANT4_EXTENSION @BxDecay0_WITH_GEANT4_EXTENSION@)

# 2021-08-30 FM: Policy defined from CMake 3.3 and up to unknown future version
if (CMAKE_VERSION VERSION_GREATER "3.3")
  cmake_policy(SET CMP0057 NEW) # interpret the IN_LIST operator in if()
endif()

@PACKAGE_INIT@

set(_BxDecay0_supported_components Geant4)

set_and_check(BxDecay0_INCLUDE_DIR @PACKAGE_BxDecay0_CONFIG_INCLUDEDIR@)

#-----------------------------------------------------------------------
# Handle paths/targets for third party deps here...
#
set(BxDecay0_GSL_VERSION @GSL_VERSION@)
set(GSL_ROOT_DIR @GSL_ROOT_DIR@)
find_package(GSL ${BxDecay0_GSL_VERSION} REQUIRED)

#-----------------------------------------------------------------------
# Include the file listing all the imported targets.
# This is installed in the same location as us...
#
if(NOT BxDecay0_TARGETS_LOADED)
  include("${CMAKE_CURRENT_LIST_DIR}/BxDecay0Targets.cmake")
  set(BxDecay0_TARGETS_LOADED 1)
endif()

set(BxDecay0_LIBRARIES BxDecay0::BxDecay0)
if (BxDecay0_WITH_GEANT4_EXTENSION)
  list(APPEND BxDecay0Geant4_LIBRARIES BxDecay0::BxDecay0_Geant4)
endif()

set(BxDecay0_FOUND True)
# Component check
foreach(_requested_comp ${BxDecay0_FIND_COMPONENTS})
  # The following line requires the CMP0057 policy set to NEW:
  if (NOT _requested_comp IN_LIST _BxDecay0_supported_components)
    set(BxDecay0_FOUND False)
    set(BxDecay0_NOT_FOUND_MESSAGE "Unsupported component: ${_requested_comp}")
  endif()
  if ("${_requested_comp}" STREQUAL "Geant4")
    if (NOT BxDecay0_WITH_GEANT4_EXTENSION)
      set(BxDecay0_Geant4_FOUND False)      
      set(BxDecay0_FOUND False)
      set(BxDecay0_NOT_FOUND_MESSAGE "Requested component ${_requested_comp} is not available")
    else()
      set(BxDecay0_Geant4_FOUND True)      
    endif()
  endif()
  
endforeach()

# - end
